plugins {
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'org.stop_lang'
version '1.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/stop-lang/stop"
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'stop'
        name = 'stop-runtime'
        userOrg = 'stop-lang'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/stop-lang/stop-runtime-java.git'
        version {
            name = version
            desc = version
            released  = new Date()
            vcsTag = version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    publish = true
}

dependencies {
    if (project.hasProperty("development")) {
        implementation project(":stop")
    }else{
        implementation 'org.stop_lang:stop:1.1.0'
    }

    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'stop-runtime'
            from components.java
            groupId = group
            version version
            artifact sourcesJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Stop Runtime for Java'
                description = 'Stop Runtime for Java'
                url = 'https://www.stop-lang.org'
                licenses {
                    license {
                        name = 'MIT LICENSE'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
}